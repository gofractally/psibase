enable_testing()

function(service name suffix)
    add_executable(${name}${suffix} ${ARGN})
    target_include_directories(${name}${suffix} PUBLIC include)
    target_link_libraries(${name}${suffix} services_system${suffix} services_user${suffix} psibase-service-simple-malloc${suffix} )
    set_target_properties(${name}${suffix} PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${ROOT_BINARY_DIR})
    if(TEST_GENERATE_SCHEMA)
        psibase_schema(${name}${suffix})
    endif()
endfunction()

function(add suffix)
    service(test-service "${suffix}" test-service.cpp)
    service(test_kv "${suffix}" test_kv.cpp)
    service(test_table "${suffix}" test_table.cpp)
    service(event-service "${suffix}" event-service.cpp)
    service(memo-service "${suffix}" memo-service.cpp)
    service(clock-service "${suffix}" clock-service.cpp)
    service(subjective-service "${suffix}" subjective-service.cpp)

    set(TEST_GENERATE_SCHEMA 1)
    service(MockCpuLimit "${suffix}" MockCpuLimit.cpp)
    service(PSubjectiveService "${suffix}" ParallelSubjectiveService.cpp)
    service(CounterService "${suffix}" CounterService.cpp)
    service(AsyncQueryService "${suffix}" AsyncQueryService.cpp)
    service(SubjectiveCounterService "${suffix}" SubjectiveCounterService.cpp)
    service(KeepSocketService "${suffix}" KeepSocketService.cpp)
    service(SocketListService "${suffix}" SocketListService.cpp)
    service(SocketAuth "${suffix}" SocketAuth.cpp)

    add_executable(psibase-tests${suffix} test.cpp test-sig.cpp test_event.cpp test_crypto.cpp test_memo.cpp test_clock.cpp test_semver.cpp test_subjective.cpp testKvMerkle.cpp test_rpc.cpp test_socket.cpp test_verify_services.cpp)
    target_include_directories(psibase-tests${suffix} PUBLIC include)
    target_link_libraries(psibase-tests${suffix} services_system${suffix} psitestlib${suffix} )
    set_target_properties(psibase-tests${suffix} PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${ROOT_BINARY_DIR})
endfunction(add)

if(DEFINED IS_WASM)
    conditional_add()
    add_wasm_test_release(psibase-tests)
    configure_file(semver.json ${ROOT_BINARY_DIR}/semver.json COPYONLY)
endif()

set(CMAKE_EXPORT_COMPILE_COMMANDS on)
